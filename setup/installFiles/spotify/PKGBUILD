pkgname=spotify-player-no-image
_pkgname=spotify-player
pkgver=0.20.4
pkgrel=1
pkgdesc="A command driven spotify player with all options enabled except for images"
arch=('x86_64' 'aarch64' 'armv7h')
url="https://github.com/aome510/spotify-player"
license=('MIT')
depends=('openssl' 'dbus' 'libpulse' 'glibc' 'gcc-libs')
conflicts=("${_pkgname}" "${pkgname}-git")
makedepends=('cargo')
source=("git+https://github.com/aome510/spotify-player.git")
sha256sums=('SKIP')
options=('!lto')

build() {
	cd "${_pkgname}"

	export RUSTUP_TOOLCHAIN=stable
	export CARGO_TARGET_DIR=target
	cargo build --release --locked --no-default-features \
		 --features notify,pulseaudio-backend,media-control,daemon,fzf
}

package() {
	cd "${_pkgname}"

	install -Dm755 target/release/spotify_player -t "${pkgdir}/usr/bin"
	install -Dm644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}